&&作者：Mulq

谁是卧底
加入\n
开始游戏\n
投票@\n
规则\n
查看玩家\n
结束游戏\n
Tips：最低四人，上限8人，请加机器人好友，否则无法私聊发送词语



规则
1：比如，在场一共有7名玩家，那么其中的6名玩家都拿的是同样的词语，而只有1名玩家拿到的词语是不同的，这名玩家就是属于卧底。\n
2：每位玩家每轮需要用自己的话去描述一下自己手中的词语（不能够直接说出词语），但是与此同时要提醒“同胞”，自己不是卧底，而卧底则要尽快的注意自己就是卧底，并且想尽一切办法隐藏住自己卧底的身份。\n
3：每轮描述完毕之后，分别投票选出自己心目中的那个卧底，票数最多的人就出局，如果卧底存活到最后两人的话则卧底获胜，反之，则其他人获胜。\n
4：请加机器人好友 否则无法私聊发送词语\n
Tips：卧底也不知道自己是卧底



结束游戏
如果:$读 %群号%/卧底/游戏房主 %QQ% 0$==0
你不是房主，无法结束游戏
返回
如果尾
$回调 游戏删除$
好的


[内部]取消游戏1
如果:$读 %群号%/卧底/开始 a 0$==1
返回
如果尾
两分钟内未开始，已删除游戏数据
$回调 游戏删除$


[内部]取消游戏2
如果:$读 %群号%/卧底/超时取消 描述 0$==0
返回
如果尾
有玩家超时未描述，已删除游戏数据
$回调 游戏删除$

[内部]取消游戏3
如果:$读 %群号%/卧底/超时取消 投票 0$==0
返回
如果尾
有玩家超时未投票，已删除游戏数据
$回调 游戏删除$


加入
如果:$读 %群号%/卧底/开始 a 0$==1
游戏已开始
返回
如果尾
如果:$读 %群号%/卧底/游戏创建 a 0$==0
$写 %群号%/卧底/游戏房主 %QQ% 1$
$写 %群号%/卧底/游戏创建 a 1$
游戏创建成功，你是房主，\n请在一分钟内开始游戏，超时取消
$调用 600000 取消游戏1$
$发送$
如果尾
a:$读 游戏/%群号%.txt 加入 []$
b:$JSON 长度 a$
c:[%b%+1]
正则:%a%==.*%QQ%.*
你已经加入游戏，不要重复加入
返回
如果尾
如果:%c%>=8
游戏人数已上限。请发送开始游戏
返回
如果尾
$JSON 添加 a %QQ%$
$写 游戏/%群号%.txt 加入 %a%$
加入成功当前人数%c%，最低四人开始游戏，上限8人




开始游戏
如果:$读 %群号%/卧底/开始 a 0$==1
游戏已开始
返回
如果尾
如果:$读 %群号%/卧底/游戏房主 %QQ% 0$==0
你不是房主，无法开始游戏
返回
如果尾
a:$读 游戏/%群号%.txt 加入 []$
b:$JSON 长度 a$
c:[%b%+1]
如果:%c%<4
最低参与人数4人，当前人数%b%
返回
如果尾
$写 %群号%/卧底/开始 a 1$
S:$随机数 0-[%b%-1]$
操:$回调 卧底词条$
$写 %群号%/卧底/词条随机 a %操%$
列:$读 %群号%/卧底/词条随机 a []$
o:$随机数 0-1$
卧:@列[%o%]
$写 %群号%/卧底/身份 @a[%S%] 卧底$
$写 %群号%/卧底/分配 @a[%S%] %卧%$
$写 %群号%/卧底/谁是卧底 a @a[%S%]$
$发送 好友 msg @a[%S%] 你的词语，【%卧%】，请在群里描述你词语$
$JSON 删除 列 %o%$
$写 %群号%/卧底/词条随机 a %列%$
数:0
麻:$读 %群号%/卧底/词条随机 a []$
:循环1
如果:%数%<%b%
判:$读 %群号%/卧底/身份 @a[%数%] 0$
如果:%判%==0
$写 %群号%/卧底/身份 @a[%数%] 平民$
$写 %群号%/卧底/分配 @a[%数%] @麻[0]$
$发送 好友 msg @a[%数%] 你的词语，【@麻[0]】，请在群里描述你的词语$
数:[%数%+1]
$跳 :循环1$
如果尾
如果:%数%<%b%
数:[%数%+1]
$跳 :循环1$
如果尾
$写 %群号%/卧底/超时取消 描述 1$
词语发送成功\n请描述你的词语来确定卧底是谁\n注：不能直接发送词语名称，否则游戏结束\n等待机器人提示后开始描述\n请在两分钟内结束本轮，超时结束游戏
$调用 8000 !!!描述开$
$调用 120000 取消游戏2$


[内部]游戏删除
$删除 /storage/emulated/0/QR/QRDic/data/%群号%/卧底$
$删除 /storage/emulated/0/QR/QRDic/data/游戏/%群号%.txt$


[内部]游戏清空
$删除 /storage/emulated/0/QR/QRDic/data/%群号%/卧底/投票人$
$删除 /storage/emulated/0/QR/QRDic/data/%群号%/卧底/被投票$
$删除 /storage/emulated/0/QR/QRDic/data/%群号%/卧底/说话$
$删除 /storage/emulated/0/QR/QRDic/data/%群号%/卧底/投票次数$
$删除 /storage/emulated/0/QR/QRDic/data/%群号%/卧底/描述说话$


[内部]游戏判断 ([0-9]+)
M:$读 游戏/%群号%.txt 加入 []$
B:0
K:0
U:$JSON 长度 M$
:循环D
如果:%K%<%U%
T:$JSON 获取 M %K%$
如果:%T%==%括号1%
%K%
返回
如果尾
K:[%K%+1]
$跳 :循环D$
如果尾






查看玩家
M:$读 游戏/%群号%.txt 加入 []$
B:0
K:0
U:$JSON 长度 M$
<<<<<<剩余玩家>>>>>\n
:循环D
如果:%K%<%U%
T:$JSON 获取 M %K%$
如果:%T%==
返回
如果尾
[%K%+1]：QQ：%T%\n昵称：$群昵称 %群号% %T%$\n
K:[%K%+1]
$跳 :循环D$
如果尾

[内部]!!!描述开
请开始描述
$写 %群号%/卧底/描述 a 1$




投票对比
排:$排行榜 %群号%/卧底/被投票 反序 10 \%0A [序号]-[键]-[序号]$
a:$取中间 @ %排%@1-@-1$
卧:$取中间 @ %排%@2-@-2$
我:$读 %群号%/卧底/被投票 %a% 0$
操:$读 %群号%/卧底/被投票 %卧% 0$
如果:%我%==%操%
重:$读 %群号%/卧底/重新开始 a 0$
$写 %群号%/卧底/重新开始 a [%重%+1]$
$回调 游戏清空$
昵称：$群昵称 %群号% %a%$\n
昵称：$群昵称 %群号% %卧%$\n
两者票数相同，请重新投票\n注:重新投票次数不得超过三次，否则游戏删除
如果尾
1:$读 %群号%/卧底/重新开始 a 0$
如果:%1%==3
重新投票次数已超过三次，游戏结束
$回调 游戏删除$
返回
如果尾
b:$读 %群号%/卧底/身份 %a% 0$
如果:%b%==卧底
QQ：%QQ%\n昵称：$群昵称 %群号% %a%$\n这憨逼就是卧底，平民玩家胜利
$回调 游戏删除$
返回
如果尾
r:$读 游戏/%群号%.txt 加入 []$
s:$回调 游戏判断 %a%$
$JSON 删除 r %s%$
$写 游戏/%群号%.txt 加入 %r%$
如果尾
o:$读 游戏/%群号%.txt 加入 []$
数:$JSON 长度 o$
如果:%数%==2
QQ：%QQ%\n昵称$群昵称 %群% %a%$\n不是卧底
$发送$
游戏人数只剩两人，游戏结束，卧底胜利\n
卧底：$读 %群号%/卧底/谁是卧底 a 0$
$回调 游戏删除$
返回
如果尾
$写 %群号%/卧底/超时取消 投票 0$
$写 %群号%/卧底/超时取消 描述 1$
$群昵称 %群% %a%$不是卧底，游戏玩家减1，请等待10秒后重新描述
$回调 游戏清空$
$调用 0 查看玩家$
$调用 10000 !!!描述开$
$调用 120000 取消游戏2$

投票@.*
如果:$读 %群号%/卧底/投票开始 a 0$==0
投票未开始
返回
如果尾
a:$读 游戏/%群号%.txt 加入 []$
b:$JSON 长度 a$
c:[%b%-1]
正则:%a%!=.*%QQ%.*
返回
如果尾
正则:%a%!=.*%AT0%.*
返回
如果尾
如果:%AT0%==
不能复读艾特
返回
如果尾
人:$读 %群号%/卧底/投票人 %QQ% 0$
如果:%人%==1
你已经投过了
返回
如果尾
被:$读 %群号%/卧底/被投票 %AT0% 0$
次:$读 %群号%/卧底/投票次数 a 0$
$写 %群号%/卧底/被投票 %AT0% [%被%+1]$
$写 %群号%/卧底/投票人 %QQ% [%人%+1]$
$写 %群号%/卧底/投票次数 a [%次%+1]$
如果尾
如果:%次%==%c%
投票已结束
$发送$
正在获取验证。。
$写 %群号%/卧底/投票开始 a 0$
$调用 5000 投票对比$
返回
如果尾
%昵称%向$群昵称 %群号% %AT0%$投了一票\n已记录


[内部]卧底词条
a:{"0":["香蕉","苹果"],"1":["状元","冠军"],"2":["饺子","包子"],"3":["端午节","中秋节"],"4":["钟","表"],"5":["高更鞋","增高鞋"],"6":["双胞胎","龙凤胎"],"7":["富二代","高富帅"],"8":["泡泡糖","棒棒糖"],"9":["甄嬛传","红楼梦"],"10":["包青天","狄仁杰"],"11":["王者荣耀","绝地求生"],"12":["自行车","电动车"],"13":["十面埋伏","四面楚歌"],"14":["近视眼镜","隐形眼镜"],"15":["菠萝密","榴莲"],"16":["唇膏","口红"],"17":["小品","相声"],"18":["手机","座机"],"19":["公交","地铁"],"20":["辣椒","芥末"],"21":["玫瑰","月季"],"22":["李白","杜甫"],"23":["星空","夜幕"],"24":["氪金","充值"],"25":["菜鸟","新手"],"26":["氪金","充值"],"27":["围巾","围脖"],"28":["小三","二奶"],"29":["酸菜鱼","水煮鱼"],"30":["蝴蝶","蜜蜂"]}
b:$随机数 0-30$
@a[%b%]


[内部]说话词语(.*)
M:$读 %群号%/卧底/描述词语 描述 0$
B:0
K:0
U:$JSON 长度 M$
:循环D
如果:%K%<%U%
T:$JSON 获取 M %K%$
如果:%T%==%括号1%
%K%
返回
如果尾
K:[%K%+1]
$跳 :循环D$
如果尾
-1



[\s\S]*
如果:%Type%==19
返回
如果尾
如果:$读 %群号%/卧底/描述 a 0$==0
返回
如果尾
a:$读 游戏/%群号%.txt 加入 []$
b:$JSON 长度 a$
c:[%b%-1]
物:$读 %群号%/卧底/分配 %QQ% 0$
正则:%a%!=[\s\S]*%QQ%[\s\S]*
返回
如果尾
正则:%参数-1%==[\s\S]*%物%[\s\S]*
$回调 游戏删除$
游戏已删除，不能发送词语名称
返回
如果尾
秒:$读 %群号%/卧底/描述说话 %QQ% 0$
如果:%秒%==1
返回
如果尾
嘎:$回调 说话词语%参数-1%$
如果:%嘎%!=-1
不能描述相同的词语，请重新描述
返回
如果尾
y:$读 %群号%/卧底/描述词语 描述 []$
$JSON 添加 y %参数-1%$
$写 %群号%/卧底/描述词语 描述 %y%$
$写 %群号%/卧底/描述说话 %QQ% 1$
说:$读 %群号%/卧底/说话 a 0$
$写 %群号%/卧底/说话 a [%说%+1]$
如果尾
如果:%说%==%c%
$写 %群号%/卧底/投票开始 a 1$
$写 %群号%/卧底/描述 a 0$
$写 %群号%/卧底/超时取消 描述 0$
$写 %群号%/卧底/超时取消 投票 1$
$调用 120000 取消游戏3$
QQ：%QQ%\n
描述：%参数-1%\n
已记录
$发送$
词语描述结束
$发送$
发送：投票@你认为的卧底\n请在两分钟内投票
返回
如果尾
QQ：%QQ%\n
描述：%参数-1%\n
已记录







